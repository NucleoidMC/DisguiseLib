plugins {
	id "architectury-plugin" version "3.4-SNAPSHOT"
	id "dev.architectury.loom" version "0.11.0-SNAPSHOT" apply false
}

repositories {
	maven {
		url 'https://jitpack.io'
	}
}

architectury {
	minecraft = rootProject.minecraft_version
}

subprojects {
	apply plugin: "dev.architectury.loom"

	dependencies {
		minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
		mappings("net.fabricmc:yarn:${rootProject.yarn_mappings}:v2")
	}

	if (it.name != "api") {

		dependencies {
			compileOnly project(":api")
			compileOnly('org.jetbrains:annotations:22.0.0')
		}

		processResources {
			from project(":api").sourceSets.main.resources
		}

		tasks.withType(JavaCompile) {
			source(project(":api").sourceSets.main.allSource)
		}
	}
}

allprojects {
	apply plugin: "java"
	apply plugin: "architectury-plugin"
	apply plugin: "maven-publish"

	archivesBaseName = rootProject.archives_base_name
	version = rootProject.mod_version
	group = rootProject.maven_group

	repositories {
		maven {
			url 'https://jitpack.io'
		}
	}

	tasks.withType(JavaCompile).configureEach {
		// ensure that the encoding is set to UTF-8, no matter what the system default is
		// this fixes some edge cases with special characters not displaying correctly
		// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
		// If Javadoc is generated, this must be specified in that task too.
		it.options.encoding = "UTF-8"

		it.options.release = 17
	}


	java {
		withSourcesJar()
	}
}
